## Amharic infinitive, agent, instrumental, manner noun
-> start

### PREFIXES

start -> stem       <aleme:>  [v=inf,+neg]

start -> stem       <me:>     [v=inf,-neg];[v=ins]

start -> stem       [:]       [v=agt]

start -> man0      [a:]       [v=man]
man0 -> man        [/:]

stem -> a          [a:]       [vc=tr]
stem -> a/         <a/:>      [vc=tr,as=rc];[vc=tr,as=it]
stem -> te         <te:>      [vc=ps,v=agt]
stem -> /C         [/:]       [vc=ps,v=inf];[vc=ps,v=ins]
stem -> |          [X/L]      [vc=ps,v=inf];[vc=ps,v=ins]
te -> |            [X/L]
a -> |             [X/L]
man0 -> |          [X/L]
stem -> tt         <t_:>      [vc=ps,v=inf];[vc=ps,v=ins]

stem -> as         <as:>      [v=agt];[v=inf];[v=ins]
as ->  ast         [t:]       [vc=tr]
ast -> aste        [e:']

start -> astM      <ast:>     [v=man]
astM -> asteM      [e:']

as -> simp          [:]       [vc=cs]
te -> simp          [:]
a -> simp           [:]       [as=smp]
stem -> simp        [:]       [vc=smp,as=smp];[vc=smp,as=it]
/C -> simp          [:]
a/ -> simp          [:]

### END

-1 -> -1C           [X/L]
-1C -> end          [:]       [v=inf];[v=man]
-1C -> -1i          [i:]      [v=agt];[v=ins]
-1i -> end          [:]       [v=agt]
-1i -> end          [a:]      [v=ins]

# Final vowels for most situations
-2V -> -1           [e:]      [v=man];[v=inf];[v=ins]
-2V -> -1           [a:]      [v=agt]

# Final L
-2V -> -1C          [:']      [v=agt];[v=man];[v=ins]
-2V -> -2V.t        [a:']     [v=inf]
-2V.t -> end        [t:]
# Final *
-2V -> -1C          [:*]      [v=agt];[v=man];[v=ins]
# infinitive and alternate for -* manner noun
-2V -> -2V.t        [e:*]     [v=man];[v=inf]

### A (CCC, C2 cannot be L, w, or y)

-2A_ -> -2V         [:]       [vc=smp];[vc=tr];[vc=ps]
-2A_ -> -2V         [_:]      [vc=cs]
-2A -> -2A_         [X!]
# Following L, y and w also possible
-3 -> -3V           [X/L]
-3V -> -2A          [e:]      [vc=ps];[vc=cs];[v=man];[v=agt,vc=smp]
# transitive agent is aC1C2aC3
-3V -> -2A          [:]       [vc=smp,v=inf];[vc=tr,v=inf];[vc=smp,v=ins];[vc=tr,v=ins];[vc=tr,v=agt]
-2AL -> -2A_        [X/L]

# initial L; no vc=tr, no as=rc possible
-3L -> -3LV         [:']      [as=it];[as=smp]
-3LV -> -2AL        [a:]

simp -> -3          [:]
as -> -3L           [:]       [vc=cs]
stem -> -3L         [:]       [vc=smp,as=smp];[vc=smp,as=it]
te -> -3L           [:]
tt -> -3L           [:]

# C1=L in CCCC; ?? apparently behaves like CCC in ps and cs (no tr)
as -> -3            [:']      [vc=cs]
te -> -3            [:']
/C -> -3            [:']
stem -> smp4L       [a:']     [v=inf,vc=smp,as=smp];[v=agt,vc=smp,as=smp];[v=ins,vc=smp,as=smp]
smp4L -> -2A        [X/L]     

### A (C*C, imperfective with w, y, L as C2; reduplicated: CaC*C)
### the same pattern of vowels works with all reduplicated forms

-3.2 -> -3V.2        [X/L]
-3.2 -> -3PV.2       [J]
-3.2 -> -3~PV.2      [~J]

# mWac, zWari
-3.2 -> -3Vw.2       [gW:g;hW:h;kW:k;qW:q]         [v=agt]
-3.2 -> -3Vw.2       [bW:b;cW:c;CW:C;dW:d;fW:f]    [v=agt]
-3.2 -> -3Vw.2       [jW:j;lW:l;mW:m;nW:n]         [v=agt]
-3.2 -> -3Vw.2       [rW:r;sW:s;SW:S]              [v=agt]
-3.2 -> -3Vw.2       [tW:t;TW:T;xW:x;zW:z]         [v=agt]
-3Vw.2 -> -1         [a:w]

# C2=' (no recip)
-3V.2 -> -1          [a:']     [as=smp];[as=it]

# memot
-3V.2 -> -1          [o:w]     [v=inf,as=smp];[v=ins,as=smp];[v=inf,as=rc];[v=ins,as=rc]
# memWamWat (labialized consonant handled in stem_dup)
-3V.2 -> -1          [a:w]     [v=man];[v=agt];[v=inf,as=it];[v=ins,as=it]

# mexeT
-3PV.2 -> -1         [e:y]     [v=inf,as=smp];[v=ins,as=smp];[v=inf,as=rc];[v=ins,as=rc]
# mexaxaT
-3PV.2 -> -1         [a:y]     [v=man];[v=agt];[v=inf,as=it];[v=ins,as=it]

# mehEd, mefEz, mefafEz
-3~PV.2 -> -1        [E:y]     [v=inf];[v=ins];[v=man]
# afiyaZ
-3~PV.2 -> -1        <iya:y>     [v=agt]

# reduplicated cases: precede by Ca
-4.2 -> -4V.2        [X]
-4V.2 -> -3.2        [a]

simp -> -3.2         [:]

# reduplicated cases
stem -> -4.2         [:]       [as=it,vc=smp]
/C -> -4.2           [:]       [as=it]
te -> -4.2           [:]       [as=it]
a/ -> -4.2           [:]       [as=it]
man -> -4.2          [:]

### B (CC_C, C2 cannot be L)

-3V -> -2B          [e:]
# can be y or w: qeyyere, lewweTe
-2B -> -2B_         [X/L]
-2B_ -> -2V         [_]        [vc=ps,v=agt];[vc=smp];[vc=cs];[vc=tr]
# Drop the gemination in passive infinitive and instrumental
-2B_ -> -2V         [:_]       [vc=ps,v=inf];[vc=ps,v=ins]

# initial L; no vc=tr
-3L -> -3LBV        [:']     [vc=smp];[vc=ps];[vc=cs]
-3LBV -> -2B        [a:]

### Quad (CCCC, C|CCC)

-2.4 -> -2V         [X/L]

-3.4 -> -3.4V       [X/L]
-3.4V -> -2.4       [:]      [as=smp]
-3.4V -> -2.4       [a]      [as=rc]

-4.4 -> -4.4V       [X/L]
-4.4V -> -3.4       [e:]
# C2=L: mebabat (no recip)
-4.4V -> -2.4       [a:']    [as=smp];[as=it]

-5.4V -> -4.4       [e:]
-5.4 -> -5.4V       [X]

simp -> -4.4        [:]       
simp -> -5.4        [:]       

ast -> -2.4         [a:']
tt -> -2.4          [a:']

# C1=L
start -> -4.4L      [:]       [vc=smp]

### 5-consonant roots
simp -> -5.5        [:]
-5.5 -> -5.5V       [X]      
# vowel following first consonant of 5-consonant verbs (wexeneggere, etc.)
-5.5V -> -4.5       [e:]
-4.5 -> -4.5V       [X/L]
-4.5V -> -3.5       [e:]
-3.5 -> -3.5V       [X/L]
-3.5V -> -2.4       [:]
-3.5V -> -2.4       [a]

### ...aCC: C (CaCC), CCaCC, C|CaCC, C|CCaCC
### including as=it versions of nearly all classes

# 'abelex<a>xet
-3aV -> -2.4        [a]
# 'abelexa<x>et
-2.4 -> -2.4_       [X]
-2.4_ -> -2V        [_]        [v=agt];[v=inf,vc=tr];[v=inf,vc=cs];[v=inf,vc=smp];[v=ins,vc=tr];[v=ins,vc=cs];[v=ins,vc=smp]
-2.4_ -> -2V        [:_]       [v=man];[v=inf,vc=ps];[v=ins,vc=ps]
-2.4_ -> -2V        [:]

# 'abele<x>axet
-3a -> -3aV         [X]

# tegfafi (need to specify the whole path from here to end)
-4a.L -> -4a.LV     [X/L]
-4a.LV -> -3a.L     [:]       [as=it,vc=ps];[as=it,vc=tr];[v=man]
-3a.L -> -3aV.L     [X]
-3aV.L -> -2.L.4    [a]
-2.L.4 -> -2.L.4_   [X]
-2.L.4_ -> -2.LV    [:]
-2.LV -> end        [i:']     [v=agt];[v=ins]
-2.LV -> -2a.LV     [a:]      [v=inf]
-2a.LV -> end       [t:']
-2.LV -> end        [:']      [v=man]

# most reduplicated verbs
# 'abe<l>exaxet; menke<r>etatet
-4a -> -4aV         [X/L]
# 'abe<>bab
-4a -> -3a          [:']      [as=smp];[as=it]
# most verbs; megel<e>babeT
-4aV -> -3a         [e:]
# mebel<e>xaxet; menker<e>tatet
-4aV -> -3a         [e:a]     [as=it];[v=man]
# 'a<g>elebabeT; 'a<b>elexaxet; men<k>eretatet
-5a -> -5aV         [X/L]
# 'ab<e>lexaxet; menk<e>retatet
-5aV -> -4a         [e:]
-6a -> -6aV         [X/L]
-6aV -> -5a         [e:]
# L in position 1, +it (smp, ps, cs)
-4aL -> -4aLV       [:']      [vc=smp]
-4aLV -> -3a        [a:]
-4aL -> -3a         [a:']    [vc=cs]
-4aL -> -3a         [:']     [vc=ps]

aste -> -3a         [:]
asteM -> -3a        [:]

| -> -3a            [:|]
| -> -4a            [:|]
| -> -4.4           [:|]
| -> -5a            [:|]

# manner CCC -> CeCaCeC, CCCC -> CeCeCaCeC
man -> -4a          [:]
# anneTaTes
man -> man4L        [:']
man4L -> man4L.     [X/L]
man4L. -> -3a       [e:]
man -> -5a          [:]
man -> -6a          [:]

simp -> -3a         [:]
simp -> -4a         [:]
# tegbabi
te -> -4a.L         [:]
# agbab
man0 -> -4a.L       [:]
# magbabat, magbabiya, agbabi
a -> -4a.L          [:]
# megbabat, megbabiya
stem -> -4a.L       [:]       [v=inf,vc=ps];[v=ins,vc=ps]
# CCCC iterative
simp -> -5a         [:]       [as=it]
# CCCCC iterative
simp -> -6a         [:]       [as=it]

# To get +it and +cs,+it for C1=L, CCC
te -> -4aL          [:]

ast -> -3a          [e:']
tt -> -3a           [e:']

end ->
