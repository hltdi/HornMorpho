DELIMITERS = "<።>" "<!>" "<?>" "<¶>";

LIST NOM = N PRON DET PROPN ;
LIST NOM2 = N PRON DET PROPN ADJ ;
LIST PREP1 = INS ABL ;
LIST PREPADV = "እንደ" "እስከ" "ስለ" "በስተ" "ያለ" "ወደ" "ባለ" ;
LIST OBLPOSTP = "ምክንያት" "<በኋላ>" "<በፊት>" "<በላይ>" "ምትክ" "ፋንታ" "ይልቅ" "ጋር" "<የተነሳ>" "<ወዲህ>";
LIST NPOSTP = "ላይ" "ውስጥ" "ታች" "ውጭ" ;
LIST POSTCONJ = "ቁጥር" "<በኋላ>" "<በፊት>" "<ድረስ>" ;
SET POSTP = OBLPOSTP | NPOSTP ;
LIST N = N ;
LIST NMOD = REL ADJ DET NUM GEN ;
SET V = (V) | (AUX) ;
# V other than jussive/imperative or AUX
SET V2 = (V !IMP) | (AUX) ;
LIST HELPING = AUX "ነበረ" "ሆነ" ;
LIST SADV = "መቸስ" "መቸም" "መቼስ" "መቼም" "እኮ" "እንጂ" "እንዴ" ;
LIST PERSON = 1 2 3 ;
LIST GENDER = FEM MASC ;
LIST NUMBER = SG PL ;
LIST SAY = "አለ" "አሰኘ" "ተሰኘ" "ተባለ" "አደረገ" ;
LIST REFL1 = (1 SG AC1S) (1 PL AC1P) (1 SG DA1S) (1 PL DA1P) (1 SG  MAL1S) (1 PL MAL1P) ;
LIST KNOWSPEAK = "አለ" "ነገረ" "ገለጸ" "ተገለጸ" "ተነገረ" "አወቀ" "ታወቀ" "አሳወቀ" "ተገነዘበ" ;

### Ambiguous POS tags
# ADPCONJ: disambiguate before noun; still ambiguous before verb
SUBSTITUTE (ADPCONJ) (ADP) PREPADV
       (1 (N)) ;

SUBSTITUTE (ADPCONJ) (SCONJ) POSTCONJ
	(-1 (V) OR (N INF)) ;

# only ቁጥር
SUBSTITUTE (PARTCONJ) (SCONJ) POSTCONJ
	 (-1 (V PFV INS)) ;

### Adpositions

# Postposition has to be preceded by a nominal.
REMOVE POSTP + (ADP)
       (NOT -1 NOM) ;

### Ambiguous postpositions/nouns like ምክንያት and postpositions/verbs like ላይ
# Postposition must be preceded by PREP+N
# ጥሩ ምክንያት
#REMOVE POSTPA
#       (-1 NMOD) ;

# በበሽታው ምክንያት
REMOVE OBLPOSTP + NOM
       (-1 NOM + PREP1) ;

# በወባ በሽታው ምክንያት
REMOVE OBLPOSTP + (N)
       (-1 (N))
       (-2 NOM2 + PREP1) ;

# በሜዳዎች ላይ
"<ላይ>" REMOVE (V)
       (-1 (OBL)) ;

# ልጆቹ ዛፍ ላይ ወጡ
"<ላይ>" REMOVE (N)
       (-1 (N))
       (-2 (N)) ;

# እቤት ውስጥ
REMOVE NPOSTP + (N)
       (-1 (OBL)) ;

# %% ጎጃም ውስጥ
REMOVE NPOSTP + (N)
       (-1 NOM)
       (NOT -1 (GEN)) ;

# የቤት ውስጥ
REMOVE NPOSTP + (ADP)
       (-1 NOM + (GEN)) ;

### Post-adposition/conjunctions
REMOVE POSTCONJ + (N)
       (-1 V + PREP1) ;

### Nouns

# %% Oblique nouns can't follow oblique nouns
REMOVE (OBL)
       (-1 (OBL)) ;

### Verbs

# Imperfective "participles" have to be followed by an aux
REMOVE (I_PART)
       (NOT  1 HELPING) ;

# Main verbs also have to be last or preceding a form of አለ or with a CCONJ like -ና
# or following a conjunction like እስከ
# This works for ፈራሽ
REMOVE (MAIN) - (CCONJ)
       (NOT 1 (<<<) OR SADV OR ("አለ"))
       (NOT -1 (SCONJ) OR (ADPCONJ)) ;

# Jussive/imperative has to be final (with possible intervening ADV like መቸስ) or preceding አለ
# or with -ና 
# Not really true: ይምጣ አይምጣ አላውቅም; but the rule accommodates this
REMOVE (IMP) - (CCONJ)
       (NOT 1 (<<<) OR SADV OR (IMP) or (MAIN) or ("አለ")) ;

# ልመጣ; remove if it isn't followed by a verb or auxiliary.
REMOVE (L_SC)
       (NOT 1 V2) ;

# %% Converb can't be last unless it's the only word. What about cleft sentences or poetry?
REMOVE (CVB)
       (1 (<<<))
       (NOT -1 (>>>)) ;

## Converbs (normally) have to be followed by another verb agreeing in PNG
## unless the sentence is a fragment (with CVB last?)
#REMOVE  (CVB $$GENDER $$NUMBER $$PERSON)
#       (NOT 1* (V $$GENDER $$NUMBER $$PERSON)) ;

# ambiguous IPFV / IMP followed by AUX must be IPFV
REMOVE (IMP)
       (1 (AUX)) ;

## rare cases where 1 subject and object is one reading
## only possible when followed by a saying verb
REMOVE REFL1
       (NOT 1* ("አለ")) ;

## A sentence shouldn't end with an oblique relative
## Instead these are adverbial clauses
## ባልዘነበ ።
REMOVE (RELC OBL)
       (1 (<<<)) ;

## If an ambiguous form of አለ is preceded by a verb, AUX or PART,
## choose the አለ reading
## Does this always work??
SELECT ("አለ")
       (-1 (AUX) OR (PART) OR (V)) ;

## 

### Ambiguous PROPN

# Proper nouns can't be preceded by noun modifiers
REMOVE (PROPN)
       (-1 NMOD) ;

# Proper nouns (አበበ) can't finish the sentence
REMOVE (PROPN)
       (1 (<<<)) ;

# Main verbs (አበበ) can't start a sentence (unless they also end the sentence or are followed by አለ)
REMOVE (V MAIN)
       (-1 (>>>))
       (NOT 1 (<<<))
       (NOT 1 ("አለ")) ;

## VERB and NOUN
# NOUN can't be last unless there's a preceding AUX (cleft sentence) or it's the only word
REMOVE NOM
       (1 (<<<))
       (NOT -1* (AUX))
       (NOT -1 (>>>)) ;

## በጣም before ADJ
"<በጣም>" SELECT (ADV)
       (1 (ADJ)) ;

## If an ambiguous particle is followed by a form of አለ/አደረግ/ተሰኘ, it's a particle
## (but in a cleft sentence it may not be followed by it directly)
## ብር ነው ያለኝ
SELECT (PART)
       (1* SAY BARRIER (V)) ;

## If an ambiguous particle is not followed by a SAYing verb, it's not a particle
REMOVE (PART)
       (NOT 1 SAY) ;

## A noun with -ና should not be followed by a verb
REMOVE NOM + (CCONJ)
       (1 V) ; 

# NADJ: disambiguate before noun?

### Complement vs. relative clause and adverbial clause
## እንደ+ before verbs like አለ ነገረ አወቀ ገለጸ and not followed by noun
REMOVE (EQTV RELC)
       (1* KNOWSPEAK BARRIER (V))
       (NOT 1 (N)) ;
REMOVE (INDE_SC ADVCL)
       (1* KNOWSPEAK BARRIER (V))
       (NOT 1 (N)) ;
## Unless there's a knowing/speaking verb later or the clause is last
REMOVE (INDE_SC CCOMP)
       (NOT 1* KNOWSPEAK)
       (NOT 1 (<<<)) ;

### Postpositions again

# what comes before ጋር must be a nominal
REMOVE (V)
       (1 ("<ጋር>" ADP)) ;

### Relative clause, adverbial clause
# Relative verbs are not followed by relative verbs
# unless there's a following noun.
REMOVE (V RELC)
       (-1 (V RELC))
       (NOT 1 (N)) ;
